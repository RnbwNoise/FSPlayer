<!DOCTYPE HTML>
<html>
    <!--
     * FragmentShader Player
     * Copyright (C) 2014 Vladimir P.
     *
     * Permission is hereby granted, free of charge, to any person obtaining a copy
     * of this software and associated documentation files (the "Software"), to deal
     * in the Software without restriction, including without limitation the rights
     * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
     * copies of the Software, and to permit persons to whom the Software is
     * furnished to do so, subject to the following conditions:
     * 
     * The above copyright notice and this permission notice shall be included in
     * all copies or substantial portions of the Software.
     * 
     * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
     * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
     * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
     * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
     * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
     * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
     * THE SOFTWARE.
    -->
    <head>
        <title>FSPlayer Sample Page</title>
        <style>
            body {
                background-color: black;
                margin: 0px;
                padding: 0px;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <script id="shader" type="x-shader/x-fragment">
            precision mediump float;
            
            uniform float time;
            uniform vec2 resolution;
            
            #define M_PI 3.1415
            
            // Textures
            
            vec3 gridTexture(vec2 uv) {
                return vec3((uv.x < 0.05 || uv.y < 0.05) ? 0.8 : 1.0);
            }
            
            vec3 checkerboardTexture(vec2 uv) {
                return vec3((uv.x < 0.5 != uv.y < 0.5) ? 0.8 : 1.0);
            }
            
            vec3 plasmaTexture(vec2 uv) {
                float f = sin(uv.x * 2.0 + time) * distance(uv, vec2(cos(time) / 2.0 + 0.5, sin(time) / 2.0 + 0.5));
                return vec3(sin(f * M_PI) / 2.0 + 0.5, cos(f * M_PI) / 2.0 + 0.5, 0.5);
            }
            
            // Transformations
            
            mat3 scale(mat3 m, vec2 scale) {
                return m * mat3(scale.x, 0.0, 0.0, 0.0, scale.y, 0.0, 0.0, 0.0, 1.0);
            }

            mat3 rotate(mat3 m, float a) {
                return m * mat3(cos(a), sin(a), 0.0, -sin(a), cos(a), 0.0, 0.0, 0.0, 1.0);
            }

            mat3 skew(mat3 m, float sx, float sy) {
                return m * mat3(1.0, tan(sy), 0.0, tan(sx), 1.0, 0.0, 0.0, 0.0, 1.0);
            }

            mat3 translate(mat3 m, vec2 translation) {
                m[2][0] += translation.x;
                m[2][1] += translation.y;
                return m;
            }

            vec2 apply(mat3 m, vec2 pt) {
                return vec2(m * vec3(pt, 1.0));
            }
            
            vec2 positionToUV(vec2 position) {
                mat3 m = mat3(1.0);
                
                m = skew(m, -0.5, 0.0);
                m = translate(m, vec2(4.0 * cos(time / 2.0), 4.0 * sin(time / 2.0)));
                m = rotate(m, time);
                m = scale(m, vec2(5.0 * (sin(time) / 2.0 + 0.5)));
                
                position = apply(m, position - 0.5);
                return position - floor(position);
            }
            
            void main() {
                vec2 position = gl_FragCoord.xy / resolution.xy;
                gl_FragColor = vec4(plasmaTexture(position) * checkerboardTexture(positionToUV(position)), 1.0);
            }
        </script>
        
        <div id="effect"></div>
        
        <script src="fsplayer.js" type="text/javascript"></script>
        
        <script type="text/javascript">
            var container = document.getElementById('effect');
            
            var player = new FSPlayer(container, 600, 600, document.getElementById('shader').textContent);
            player.setQuality(1/2);
            
            container.onclick = function(event) {
                if(player.isPlaying())
                    player.pause();
                else
                    player.play();
            };
            
            player.play();
        </script>
    </body>
</html>